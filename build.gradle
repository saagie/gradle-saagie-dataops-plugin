plugins {
    id 'net.researchgate.release' version '2.6.0'
    id 'io.codearte.nexus-staging' version '0.11.0'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'org.sonarqube' version '2.6.2'
    id 'com.adarshr.test-logger' version '1.1.2'
    id 'java-gradle-plugin'
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group 'io.saagie'
version version
description "Gradle plugin to manage Saagie's DataFabric jobs with gradle."

repositories {
    jcenter()
    mavenCentral()
}

configurations {
    compile.exclude module: 'slf4j-impl'
    testCompile.exclude module: 'groovy-all'
    testCompile.exclude module: 'slf4j-impl'
    pom
}

dependencies {
    compile gradleApi()
    compile 'com.squareup.okhttp3:okhttp:4.0.0'

    testCompile gradleTestKit()
    testCompile 'junit:junit:4+'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-2'
    testCompile 'com.squareup.okhttp3:mockwebserver:4.0.0'
}

test.testLogging.showStandardStreams = true

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar, javadocJar
}

signing {
    required signatory != null
    sign configurations.archives
}

release {
    preTagCommitMessage = '[ci skip] pre tag commit:'
    newVersionCommitMessage = '[ci skip] new version commit:'
}

task mergeToDevelop(type: Exec) {
    description 'Merge master to develop'
    commandLine 'git', 'merge', 'master'
}

task switchToDevelop(type: Exec) {
    description 'Checkout develop'
    commandLine 'git', 'checkout', 'develop'
}

task prepareDocCommit(type: Exec) {
    description 'Replace version number in documentation'
    commandLine 'git', 'add', 'README.md', 'gradle.properties'
}

task commitVersion(type: Exec, dependsOn: prepareDocCommit) {
    description 'Commit version'
    commandLine 'git', 'commit', '-m', '[ci skip] replace readme version.'
}

task patch(type: Task) {
    doLast {
        def major = version.substring(0, version.lastIndexOf('.') + 1)
        def updatedVersion = "${major}${buildNumber}"
        logger.quiet("Patch update from $version to $updatedVersion")
        def properties = new File('gradle.properties')
        properties.write(properties.text.replace("version=$version", "version=$updatedVersion"))
    }
}

task minor(type: Task) {
    doLast {
        def minor = version.substring(version.lastIndexOf('.') + -1, version.lastIndexOf('.'))
        def m = minor.toInteger() + 1
        def major = version.substring(0, version.indexOf('.'))
        def patch = version.substring(version.lastIndexOf('.'))
        def updatedVersion = "${major}.${m}${patch}"
        logger.quiet("Minor update from $version to $updatedVersion")
        def properties = new File('gradle.properties')
        properties.write(properties.text.replace("version=$version", "version=$updatedVersion"))
    }
}

task major(type: Task) {
    doLast {
        def major = version.substring(0, version.indexOf('.'))
        def m = major.toInteger() + 1
        def minor = version.substring(version.lastIndexOf('.') + -1, version.lastIndexOf('.'))
        def patch = version.substring(version.lastIndexOf('.'))
        def updatedVersion = "${m}.${minor}${patch}"
        logger.quiet("Major update from $version to $updatedVersion")
        def properties = new File('gradle.properties')
        properties.write(properties.text.replace("version=$version", "version=$updatedVersion"))
    }
}

mergeToDevelop.dependsOn switchToDevelop

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/${project.version.endsWith('-SNAPSHOT') ? 'content/repositories/snapshots' : 'service/local/staging/deploy/maven2'}") {
                authentication(userName: project.findProperty('nexusName') ?: '', password: project.findProperty('nexusPassword') ?: '')
            }

            pom.project {
                name project.name
                url 'https://github.com/saagie/gradle-saagie-dataops-plugin'
                packaging 'jar'
                description project.description

                scm {
                    url 'scm:git@github.com:saagie/gradle-saagie-dataops-plugin.git'
                    connection 'scm:git@github.com:saagie/gradle-saagie-dataops-plugin.git'
                    developerConnection 'scm:git@github.com:saagie/gradle-saagie-dataops-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'decampsrenan'
                        name 'Renan Decamps'
                        url 'https://github.com/decampsrenan'
                    }
                }
            }
        }
    }
}

publishPlugins.dependsOn uploadArchives
afterReleaseBuild.dependsOn publishPlugins

nexusStaging {
    username = project.findProperty('nexusName')
    password = project.findProperty('nexusPassword')
    delayBetweenRetriesInMillis = 10000
}

gradlePlugin.plugins {
    saagiePlugin {
        id = 'io.saagie.gradle-saagie-dataops-plugin'
        implementationClass = 'io.saagie.plugin.DataOpsPlugin'
    }
}

// ===============
// Publishing
pluginBundle {
    website = 'https://www.saagie.com/'
    vcsUrl = 'https://github.com/saagie/gradle-saagie-dataops-plugin'
    description = project.description
    tags = ['saagie', 'data fabric']

    plugins {
        saagiePlugin {
            id = 'io.saagie.gradle-saagie-dataops-plugin'
            displayName = 'Gradle Saagie plugin'
        }
    }
}

jacocoTestReport.dependsOn(test)
tasks.getByName('sonarqube').dependsOn(jacocoTestReport)

task createPluginClasspath {
    def outputDir = file("${buildDir}/resources/test")

    inputs.files sourceSets.test.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("${outputDir}/plugin-classpath.txt").text = sourceSets.test.runtimeClasspath.join('\n',)
    }
}

test.dependsOn(createPluginClasspath)

testlogger {
    theme 'mocha'
}
