plugins {
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'groovy'
    id 'jacoco'
    id 'net.researchgate.release' version '2.6.0'
    id 'io.codearte.nexus-staging' version '0.21.0'
    id 'com.adarshr.test-logger' version '1.7.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group 'io.saagie'
version version
description "Gradle plugin to manage Saagie's DataFabric jobs with gradle."

repositories {
    jcenter()
    mavenCentral()
}

configurations {
    compile.exclude module: 'slf4j-impl'
    testCompile.exclude module: 'groovy-all'
    testCompile.exclude module: 'slf4j-impl'
}

dependencies {
    compile gradleApi()
    compile 'com.squareup.okhttp3:okhttp:4.0.0'

    testCompile gradleTestKit()
    testCompile 'junit:junit:4+'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-2'
}

configurations {
    pom
}

test {
    testLogging {
        showStandardStreams = true
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar, javadocJar
}

release {
    preTagCommitMessage = '[ci skip] pre tag commit:'
    newVersionCommitMessage = '[ci skip] new version commit:'
}

task mergeToDevelop(type: Exec) {
    description 'Merge master to develop'
    commandLine 'git', 'merge', 'master'
}

task switchToDevelop(type: Exec) {
    description 'Checkout develop'
    commandLine 'git', 'checkout', 'develop'
}

task replaceVersionInFiles(type: Task) {
    doLast {
        def readMe = new File('README.md')
        readMe.write(readMe.text.replace(previousVersion, version.toString().replace('-SNAPSHOT', '')))
        def properties = new File('gradle.properties')
        properties.write(properties.text.replace(previousVersion, version.toString().replace('-SNAPSHOT', '')))
    }
}

task prepareDocCommit(type: Exec, dependsOn: replaceVersionInFiles) {
    description 'Replace version number in documentation'
    commandLine 'git', 'add', 'README.md', 'gradle.properties'
}

task commitVersion(type: Exec, dependsOn: prepareDocCommit) {
    description 'Commit version'
    commandLine 'git', 'commit', '-m', '[ci skip] replace readme version.'
}

mergeToDevelop.dependsOn switchToDevelop
updateVersion.dependsOn commitVersion

publishing {
    repositories {
        maven {
            url = "https://oss.sonatype.org/${project.version.endsWith('-SNAPSHOT') ? 'content/repositories/snapshots' : 'service/local/staging/deploy/maven2'}"
            credentials {
                username = project.findProperty('nexusName')
                password = project.findProperty('nexusPassword')
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact javadocJar

            pom {
                name = project.name
                url = 'https://github.com/saagie/gradle-saagie-dataops-plugin'
                description = project.description

                scm {
                    url = 'scm:git@github.com:saagie/gradle-saagie-dataops-plugin.git'
                    connection = 'scm:git@github.com:saagie/gradle-saagie-dataops-plugin.git'
                    developerConnection = 'scm:git@github.com:saagie/gradle-saagie-dataops-plugin.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = "decampsrenan"
                        name = "Renan Decamps"
                        url = "https://github.com/decampsrenan"
                    }
                }
            }
        }
    }
}

publishPlugins.dependsOn publishing
afterReleaseBuild.dependsOn publishPlugins


nexusStaging {
    username = project.findProperty('nexusName')
    password = project.findProperty('nexusPassword')
    delayBetweenRetriesInMillis = 10000
}

gradlePlugin {
    plugins {
        saagieDataopsPlugin {
            id = 'io.saagie.gradle-saagie-dataops-plugin'
            implementationClass = 'io.saagie.plugin.DataOpsPlugin'
        }
    }
}

// ===============
// Publishing

pluginBundle {
    website = 'https://www.saagie.com/'
    vcsUrl = 'https://github.com/saagie/gradle-saagie-dataops-plugin'
    description = project.description
    tags = ['saagie', 'data fabric']

    plugins {
        saagieDataopsPlugin {
            id = 'io.saagie.gradle-saagie-dataops-plugin'
            displayName = 'Graadle Saagie plugin'
            description = project.description
        }
    }
}

task createPluginClasspath {
    def outputDir = file("${buildDir}/resources/test")

    inputs.files sourceSets.test.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("${outputDir}/plugin-classpath.txt").text = sourceSets.test.runtimeClasspath.join('\n',)
    }
}

test.dependsOn(createPluginClasspath)

testlogger {
    theme 'mocha'
}
